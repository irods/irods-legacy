bcAnnotateBeFilesRule {
# Command to be executed located in irods/server/bin/cmd/identify_filenames
# This invokes a Python code which generates an annotated text file for each of
# the Feature files generated by the Bulk Extractor.
# Input Parameters are:
#   Image File path, 
#   Bulk_extractor directory
# Output PArameter is: 
#   Output directory annotatedFilesDir to store the annotated files.
# Tool: identify_filenames --all --imagefile
#   "path/to/imagefile.aff" "Path/to/beFeatDir" "Path/to/outAnnDir"
# Example: 
#  irule -F rulemsiBcAnnotateBeFiles.r "*image='/path/to/image.aff'" \
# "*beOutDir='/path/to/beDir'" "*annotateFilesDir='/path/to/newdir'"
#  
    *Cmd="identify_filenames";
    #*Arg="--all --imagefile" ++ " *image" ++ " *beFeatDir" ++ " *annotateFilesDir";
    *timeStamp = double (time());

    # Query the metadata catalog
    # First get the DataID of the image. Fail if the image doesn't exist
    msiIsData(*image, *DataID, *Status);
    if (int(*DataID) == 0) {
        writeLine("stdout", "Please enter a filename");
        fail;
    }

    # Image exists

    # Now Make a query to get the path to the image and the resource name 
    # DATA_PATH: Physical path name for digital object in resource
    # DATA_RESC_NAME: Logical name of storage resource
    *Query = select DATA_PATH, DATA_RESC_NAME where DATA_ID = '*DataID';
    foreach (*row in *Query) {
        *PathToImageFile = *row.DATA_PATH;
        *Resource = *row.DATA_RESC_NAME;
        writeLine("stdout", "D: Path to Image file = *PathToImageFile, Resource= *Resource");
 
    }

    # Make another query for IP Address of the resource
    # RESC_LOC: Resource IP Address
    # DATA_RESC_NAME: Logical name of storage resource
    *Query2 = select RESC_LOC where DATA_RESC_NAME = '*Resource';
    foreach (*row in *Query2) {
        *Addr = *row.RESC_LOC;
        writeLine("stdout", "D: Host Name = *Addr, Resource= *Resource");
    }

    # Query the metadata catalog for the input directory beFeatDir
    # First get the Colection ID (CollID_beFeat) of the image. Fail if the 
    # directory doesn't exist
    msiIsColl(*beFeatDir, *CollID_beFeat, *Status_beFeat);
    if (int(*CollID_beFeat) == 0) {
        writeLine("stdout", "Please enter a Directory name for BE Features input");
        fail;
    }

    # Get the physical path for the directory/collection from DATA_PATH
    *Query3 = select DATA_PATH where COLL_ID = '*CollID_beFeat';

    foreach (*row3 in *Query3) {
        *FeatFile = *row3.DATA_PATH;

        # The collection name is the same for all the files. So get the 
        # collection name for the first one and break out of the loop. 
        msiSplitPath(*FeatFile, *Coll, *File);

        writeLine("stdout", "D: Feature Directory: PathToFeatDir = *Coll");
        *PathToFeatDir = *Coll;
        break;
    }

    writeLine("stdout", "D: Collection Directory = *PathToFeatDir");

    # To have a unique name for the generated file, we will use a prefix
    # with timestamp and user-id. The unique file will be generatd in /tmp
    # directory, which will later be copied to the datagrid.
    *prefixStr = "*timeStamp$userNameClient";
    *tempStr = "/tmp/*prefixStr" ++ "outAnnDir";

    *Arg1 = "--all"
    *Arg2 = "--imagefile"
    *Arg3 = execCmdArg(*PathToImageFile);    # Image:Input
    *Arg4 = execCmdArg(*PathToFeatDir);      # FeatDir: Input
    *Arg5 = execCmdArg(*tempStr); # Output Feature Directory

    writeLine("stdout","Running identify_filenames Command...");
    writeLine ("stdout","Command: *Cmd *Arg1 *Arg2 *Arg3 *Arg4 *Arg5");

    if (errorcode(msiExecCmd(*Cmd,"*Arg1 *Arg2 *Arg3 *Arg4 *Arg5","null","*image","null",*Result)) < 0) {
        if(errormsg(*Result,*msg)==0) { 
            msiGetStderrInExecCmdOut(*Result,*Out); 
            writeLine("stdout", "ERROR: *Out");
        } else {
            writeLine("stdout", "Result msg is empty");
        }
    } else {
        # Command executed successfully
        msiGetStdoutInExecCmdOut(*Result,*Out);

        # Display the output from the command
        writeLine("stdout", "Command Output: \n *Out ");
        
        # hintPath is the 4th argument of msiExecCmd, which is a file on the grid
        # The command will be executed on the host where this file is stored. 
        *hintPath = "*beFeatDir"++"/report.xml";
        #writeLine("stdout", "D: hintPath: *hintPath");

        # Copy the files from /tmp to the specified output directory
        copyFiles(*Addr, *tempStr, *outAnnDir, *prefixStr, *CollID_beFeat, *hintPath, *status);
        # Clean up the temporary files
        cleanup(*Addr, *tempStr, *outAnnDir, *prefixStr, *status); 
    }

}

# Function: copyFiles()
# copyFiles is called for copying the files in the newly generated directory in
# /tmp to the datagrid.

copyFiles: input string * input string * input string * input string * input integer * input string * output integer -> integer 
copyFiles(*Addr, *tempStr, *outAnnDir, *prefixStr, *CollID_beFeat, *hintPath, *status) {
    
    writeLine("stdout", "copyFiles: Moving *tempStr to *outAnnDir"); 
    *tempStr = *tempStr++'/';

    remote(*Addr, "null") {

        # First make a directory outAnnDir in the grid by calling a 
        # shell script: bcMkdir
        *Arg1 =  execCmdArg(*outAnnDir);          
        writeLine("stdout", "D: Calling script bcMkdir with arg *Arg1");
        writeLine("stdout", "copyFiles: *Addr, *tempStr, *outAnnDir, *prefixStr, *CollID_beFeat");
        #msiExecCmd("bcMkdir", *Arg1, "null", "null", "null", *Result); 
        msiExecCmd("bcMkdir", *Arg1, "null", "*hintPath", "null", *Result);

        # Get the list of the annotated files and copy one by one
        # Shell script bcListDir is used to list the files in the
        # temporary directory created in /tmp
        *a1 = execCmdArg(*tempStr);
        ## writeLine("stdout", "D: Calling script bcListDir with arg *a1");

        #msiExecCmd("bcListDir",*a1, "null", "null", "null", *Result);
        msiExecCmd("bcListDir",*a1, "null", "*hintPath", "null", *Result);
        msiGetStdoutInExecCmdOut(*Result, *Out);

        # Call split to put the listed files in an array
        *a = split(*Out, "\n");
        ## writeLine("stdout", "D: Files in the array are  *a ");

        foreach (*file in *a) {
            *file = *tempStr++*file;
            msiSplitPath(*file, *Coll, *just_file);
 
            *local = "localPath=*tempStr++++forceFlag=";

            # Copy the files from the /tmp/*annOutDir location to the grid
            *new_outAnnDir = *outAnnDir++"/"++*just_file;

            # Call msiDataObjPut to do the copy of the file
            ## writeLine("stdout", "D: copyFiles: Copying to file new_outAnnDir: *new_outAnnDir");
            msiDataObjPut(*new_outAnnDir, "null", *local, *status);
        }

    } 
}

# Function: cleanup: Calls a script to remove the temporary files created
# in /tmp
cleanup: input string * input string * input string * input string * output integer -> integer 
cleanup(*Addr, *tempStr, *outAnnDir, *prefixStr, *status) {

       writeLine("stdout", "Cleanup: Removing temporary files"); 
       remote(*Addr, "null") {
            *Arg1 =  execCmdArg(*prefixStr);          
            msiExecCmd("tmpCleanup", *Arg1, "null", "null", "null", *Result); 
       }
}

INPUT *image="/AstroZone/home/pixel/bcfiles/charlie-work-usb-2009-12-11.aff", *beFeatDir="/AstroZone/home/pixel/bcfiles/beFeatDir", *outAnnDir="/AstroZone/home/pixel/bcfiles/outAnnDir"
OUTPUT ruleExecOut
